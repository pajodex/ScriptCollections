// [========================================================]
// [============    Battle Arena - Touch Down    ==========]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - The Touch Down System				    ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [                                                        ]
// [========================================================]
// [================= Edited by: pajodex ===================]
// [ Edit changelog:
// [--------------------------------------------------------] 
// [	~ Minor bug fixes									]
// [	~ Converted to more rA friendly script				]
// [	~ Added mapwarp to remove all players in map		]
// [	~ Added Easier configurations						]
// [	~ Added auto team cloth color change				]
// [                                                        ]
// [========================================================]

pvp_n_3-1,93,182,0	script	Red Goal::alatdrg	4_FAIRYDEADLEAF,2,2,{
	end;
	
OnTouch:
	if(getcharid(3) == $tdWearedId)
	{
		.@red = 0;
		if (callfunc("CheckTDTeamRedPlayer") == 1)
		{
			.@red = 1;
		}
		if(.@red == 0)
		{
			//show ball
			specialeffect 885;	//goal effect
			mapannounce $touchdownMap$, "TOUCH DOWNNNNN!!!!!!! " + strcharinfo(0) + " grabs " + $pointPerTouchDown + " points for his team!", bc_all, 0x0000FF;
			$teamBlueTDScore = $teamBlueTDScore + $pointPerTouchDown;
      bg_updatescore($touchdownMap$, $teamBlueTDScore, $teamRedTDScore);
			$tdWearedId = -1;
			sleep 5000;
			donpcevent "alatdb::OnResetBall";
		}
	}
	end;
}

pvp_n_3-1,94,15,0	script	Blue Goal::alatdbg	4_FAIRYDEADLEAF,2,2,{
	end;
	
OnTouch:
	if(getcharid(3) == $tdWearedId)
	{
		.@red = 0;
		if (callfunc("CheckTDTeamRedPlayer") == 1)
		{
			.@red = 1;
		}
		if(.@red == 1)
		{
			//show ball
			specialeffect 882;	//goal effect
			mapannounce $touchdownMap$, "TOUCH DOWNNNNN!!!!!!! " + strcharinfo(0) + " grabs " + $pointPerTouchDown + " points for his team!",  bc_all, 0xFF0000;
			$teamRedTDScore = $teamRedTDScore + $pointPerTouchDown;	
      bg_updatescore($touchdownMap$, $teamBlueTDScore, $teamRedTDScore);
			$tdWearedId = -1;
			sleep 5000;
			donpcevent "alatdb::OnResetBall";
		}
	}
	end;
}

pvp_n_3-1,106,100,0	script	Poring Ball::alatdb	4_NECORING,2,2,{
	end;
	
OnTouch:
	if($tdWearedId == -1 && $touchdownon == 2;)
	{
		$tdWearedId = getcharid(3);
		$wearerRed = 0;
		if (callfunc("CheckTDTeamRedPlayer") == 1)
		{
			$wearerRed = 1;
		}
		setnpcdisplay strnpcinfo(3), HIDDEN_WARP_NPC;
		.ballVisible = 0;
	}
	end; 
	
OnBlockStartPoints:
  .blocked = 1;
	setcell($touchdownMap$, 121,182,116,171, cell_walkable, false);
  setcell($touchdownMap$, 111,16,116,24, cell_walkable, false);
  return;
	
OnUnblockStartPoints:
  .blocked = 0;
	setcell($touchdownMap$, 121,182,116,171, cell_walkable, true);
	setcell($touchdownMap$, 111,16,116,24, cell_walkable, true);
	set .ballVisible, 1;
	return;
	
OnResetBall:
	setnpcdisplay strnpcinfo(3), HIDDEN_WARP_NPC;
	//reset ball position
	movenpc(strnpcinfo(3),106,100);
	//reset players positions
	bg_warp($@blueTeam_TD, $touchdownMap$, $warpPointBlueTD[0], $warpPointBlueTD[1]);
	bg_warp($@RedTeam_TD, $touchdownMap$, $warpPointRedTD[0], $warpPointRedTD[1]);
	//reset wearer id
	$tdWearedId = -1;
	//block starting points
	callsub OnBlockStartPoints;
	mapannounce $touchdownMap$, "[Touch Down] Next round start in " + $delayBetweenRounds + " seconds!",  bc_all, 0x00FF00;
	//special effect timer
	.countTimer = 0;
	initnpctimer;
	end;
	
OnMoveBallToDeadPlace:
	movenpc(strnpcinfo(3),$deadPlaceX, $deadPlaceY);
	setnpcdisplay strnpcinfo(3), 4_NECORING;
	end;
	
OnTimer1000:
	if($touchdownon != 2) { 
		detachrid; 
		end; 
		} 
	.countTimer = .countTimer + 1;
	if(.countTimer == $delayBetweenRounds)
	{
		mapannounce $touchdownMap$, "[Touch Down] Round Start!", bc_yellow;
		callsub OnUnblockStartPoints;
		setnpcdisplay strnpcinfo(3), 4_NECORING;
	}
	else if(.countTimer % 5 == 0 && .blocked == 1)
	{
		mapannounce $touchdownMap$, "[Touch Down] Round Start in " + ($delayBetweenRounds - .countTimer) + "s",  bc_all, 0x00FF00;
	}
	
	if(.ballVisible == 1)
	{
		specialeffect 432;
	}
	else if($tdWearedId != -1)
	{
		attachrid($tdWearedId);
		if($wearerRed == 1)
		{
			//red
			specialeffect2 521;
		}
		else
		{
			//blue
			specialeffect2 830;
		}
	}
	setnpctimer 0;
	end;
	
OnInit:
	npcspeed 0;
	.blocked = 0;
	end;
}

pvp_n_3-1,99,144,0	script	Touch DownManager::alabatd	HIDDEN_WARP_NPC,2,2,{
	end;

OnStartTD:
	$touchdownon = 2;
	pvpon $touchdownMap$;
	$teamRedTDScore = 0;
	$teamBlueTDScore = 0;
	.countTimer = 0;
	$tdWearedId = -1;
	initnpctimer;
	donpcevent "alatdb::OnResetBall";
	end;

OnEndTD:
	pvpoff $touchdownMap$;
	enablenpc "alatdb";
	enablenpc "alatdbg";
	enablenpc "alatdrg";
	if ($teamBlueTDScore == $teamRedTDScore)
	{
		announce "[Touch Down] It's a Draw with a score of " + $teamBlueTDScore + " to " + $teamRedTDScore + "! Congratulations everybody!",  bc_all, 0x00FF00;
		
		for(.@i = 0; .@i<getarraysize($@tdTeamBlue); .@i++)
		{
			if(attachrid($@tdTeamBlue[.@i]))
			{
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[2];
				if(.@cnt < .rewardMin[2])
					.@cnt = .rewardMin[2];
				getitem .reward, .@cnt;
				Zeny = Zeny + $TDEntryPrice * ($maxTDMemberCount - 1) / (getarraysize($@tdTeamBlue) + getarraysize($@tdTeamRed));
			}
		}
		for(.@i = 0; .@i<getarraysize($@tdTeamRed); .@i++)
		{
			if(attachrid($@tdTeamRed[.@i]))
			{
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[2];
				if(.@cnt < .rewardMin[2])
					.@cnt = .rewardMin[2];
				getitem .reward, .@cnt;
				Zeny = Zeny + $TDEntryPrice * ($maxTDMemberCount - 1) / (getarraysize($@tdTeamBlue) + getarraysize($@tdTeamRed));
			}
		}
	}
	else if ($teamBlueTDScore > $teamRedTDScore)
	{
		announce "[Touch Down] Blue Team wins the Touch Down with a score of " + $teamBlueTDScore + " to " + $teamRedTDScore + "! Congratulations!",  bc_all, 0x00FF00;
		for(.@i = 0; .@i<getarraysize($@tdTeamBlue); .@i++)
		{
			if(attachrid($@tdTeamBlue[.@i]))
			{
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[0];
				if(.@cnt < .rewardMin[0])
					.@cnt = .rewardMin[0];
				getitem .reward, .@cnt;
				//share zeny price
				Zeny = Zeny + $TDEntryPrice * ($maxTDMemberCount - 1) / getarraysize($@tdTeamBlue);
				TDPOINTS = TDPOINTS + .pointPerVictory;
				Zeny = Zeny + TDPOINTS * 10000;
			}
		}
		for(.@i = 0; .@i<getarraysize($@tdTeamRed); .@i++)
		{
			if(attachrid($@tdTeamRed[.@i]))
			{
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[1];
				if(.@cnt < .rewardMin[1])
					.@cnt = .rewardMin[1];
				getitem .reward, .@cnt;
			}
		}
	}
	else
	{
		announce "[Touch Down] Red Team wins the Touch Down with a score of " + $teamRedTDScore + " to " + $teamBlueTDScore + "! Congratulations!",  bc_all, 0x00FF00;
		for(.@i = 0; .@i<getarraysize($@tdTeamRed); .@i++)
		{
			if(attachrid($@tdTeamRed[.@i]))
			{
				setlook 7, @revert;
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[0];
				if(.@cnt < .rewardMin[0])
					.@cnt = .rewardMin[0];
				getitem .reward, .@cnt;
				//share zeny price
				Zeny = Zeny + $TDEntryPrice * ($maxTDMemberCount - 1) / getarraysize($@tdTeamRed);
				getitem .reward, TDPOINTS / 10;
				TDPOINTS = TDPOINTS + .pointPerVictory;
				Zeny = Zeny + TDPOINTS * 10000;
			}
		}
		for(.@i = 0; .@i<getarraysize($@tdTeamBlue); .@i++)
		{
			if(attachrid($@tdTeamBlue[.@i]))
			{
				setlook 7, @revert;
				bg_leave;
				.@cnt = TDPOINTS / .rewardCnt[1];
				if(.@cnt < .rewardMin[1])
					.@cnt = .rewardMin[1];
				getitem .reward, .@cnt;
			}
		}
	}
	mapwarp $touchdownMap$,"prontera",156,191,0;
	bg_destroy $@RedTeam_TD; 
	bg_destroy $@blueTeam_TD; 
	$touchdownon = 0;
	end;	

OnTimer60000:
	//every minute, check for members availability
	.countTimer = .countTimer + 1;	
	if (.countTimer == $battleDuration)
	{
		goto OnEndTD;
	}
	setnpctimer 0;
	end;

OnPCLoginEvent:
	TDPOINTS = 0;
	end;
	
OnPCDieEvent:
	if (strcharinfo(3) == $touchdownMap$)
	{
		if(getcharid(3) == $tdWearedId)
		{
			$tdWearedId = -1;
			getmapxy(.@map$, $deadPlaceX, $deadPlaceY, UNITTYPE_PC);
			donpcevent "alatdb::OnMoveBallToDeadPlace";
		}
	
		if (callfunc("CheckTDTeamRedPlayer") == 1)
		{
			//killed player was in first team
			//increase team 2 score
			$teamBlueTDScore = $teamBlueTDScore + .pointPerKill;
			addtimer 3000, strnpcinfo(3) + "::OnHealAfterDeath";
		}
		else if (callfunc("CheckTDTeamBluePlayer", 1) == 1)
		{
			//killed player was in second team
			//increase team 1 score
			$teamRedTDScore = $teamRedTDScore + .pointPerKill;
			addtimer 3000, strnpcinfo(3) + "::OnHealAfterDeath";
		}

		attachrid(killerrid);
		TDPOINTS = TDPOINTS + .pointPerKill;
		dispbottom "[Touch Down] You earn " + .pointPerKill + " points for killing a player. Your current balance is " + TDPOINTS;
    bg_updatescore($touchdownMap$, $teamBlueTDScore, $teamRedTDScore);
	}
	end;
	
OnHealAfterDeath:
	percentheal 100, 100;
	if(.buffs == 1)
	{
		specialeffect2 EF_INCREASEAGI; sc_start SC_INCREASEAGI,240000,10;
		specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	}
	dispbottom "[Valkyrie] You received the War Goddess benediction!";
	end;
	
OnBoardAsked:
	.@caller = getcharid(3);
	dispbottom "!!Touch Down Points Board!!";
	dispbottom "";
	dispbottom "Team Red total Points ==> " + $teamRedTDScore;
	for(.@i = 0; .@i<getarraysize($@tdTeamRed); .@i++)
	{
		.@attached = attachrid($@tdTeamRed[.@i]);
		if (.@attached == 0)
		{
			deletearray $@tdTeamRed[.@i], 1;			
		}
		else
		{
			.@message$ = "	~" + strcharinfo(0) + ": " + TDPOINTS;
			if(attachrid(.@caller))
				dispbottom .@message$;
		}
	}
	
	dispbottom "";
	dispbottom "Team Blue total Points ==> " + $teamBlueTDScore;
	for(.@i = 0; .@i<getarraysize($@tdTeamBlue); .@i++)
	{
		.@attached = attachrid($@tdTeamBlue[.@i]);
		if (.@attached == 0)
		{
			deletearray $@tdTeamBlue[.@i], 1;			
		}
		else
		{
			.@message$ = "	~" + strcharinfo(0) + ": " + TDPOINTS;
			if(attachrid(.@caller))
				dispbottom .@message$;
		}
	}
	end;

OnInit:
	// Config starts here ----------------------
	$maxTDMemberCount = 8;				// Max players to start (if 4v4, settings should be = 8)
	$TDEntryPrice = 10000;				// Zeny entry fee
	.pointPerKill = 1;					// Points gained per kill
	$pointPerTouchDown = 5;				// Points gained per touch down
	.pointPerVictory = 10;				// Points gained per victory
	.reward = 7773;						// Reward ID
	setarray .rewardMin[0], 5, 1;		// Reward amt <WIN TEA>, <LOSE TEAM>
	setarray .rewardCnt[0], 10, 20;		// Reward count
	setarray $TDDuration[0], 1, 10, 15; // Game time in minutes
	.buffs = 1;							// Buff after death ( 1 = on ; 0 = off )
	.redteam_color = 412;				// Red team cloth color
	.blueteam_color = 300;				// Blue team cloth color
	set $delayBetweenRounds, 2; // Delays between rounds (in seconds)
	// Config ends here --------------------------
	
	// Dont touch these part below if you dont know what your doing
	$touchdownMap$ = "pvp_n_3-1";
	$touchdownon = 0;
	setarray $@tdTeamBlue[0], 0;
	setarray $@tdTeamRed[0], 0;
	setarray $warpPointRedTD[0], 123, 179;
	setarray $warpPointBlueTD[0], 118, 18;
	bindatcmd "tdboard", strnpcinfo(3) + "::OnBoardAsked";
	mapwarp $touchdownMap$,"prontera",156,191,0;
	disablenpc "alatdb";
	disablenpc "alatdbg";
	disablenpc "alatdrg";
	end;
}

pvp_n_3-1	mapflag	battleground	2

prontera,128,160,0	script	Touch Down::alatdent	4_M_CATMAN1,{
	.@npcname$ = "[Leag]";
	mes .@npcname$;
	mes "Hello to you, " + strcharinfo(0);
	mes "I'm the Touch Down Manager.";
	mes "When you talk to me, you'll be able to setup or join an existing Touch Down.";
	@revert = getlook(7);
	next;
	mes .@npcname$;
	mes "Here's how a Touch Down works.";
	mes "First of all, this is a team match.";
	mes "A general party matching will be made when all players will be registered.";
	next;
	mes .@npcname$;
	mes "Once " + $maxTDMemberCount + " will have join, the team matching will begin, and the match will start.";
	mes "This is not really an alone fight.";
	.@message$ = "";
	for(.@i = 0; .@i<getarraysize($TDDuration); .@i++)
	{
		.@message$ = (.@i == 0 ? "" : (.@i == getarraysize($TDDuration) - 1 ? ", or " : ", ")) + $TDDuration[.@i];
	}
	mes "Each team will have a duration of " + .@message$ + " minutes to bring as much Poring as possible to the Goal of the other team.";
	mes "Normal kills gives points, but as long as a Poring to the Goal will brings much more points than killing.";
	next;
	mes .@npcname$;
	mes "So. Are you interested?";
	if ($touchdownon == 2)
	{
		goto OnEnterAgain;
	}
	if ($touchdownon == 1)
	{
		if(callfunc("CheckTDPlayers") == 1)
		{
			next;
			mes .@npcname$;
			mes "Be patient please. The Touch Down will start soon.";
			close;
		}
		
		if (select("Join the currently running Touch Down","No, thanks") == 1)
		{
			callsub OnJoin, 1;
		}
		else
		{
			goto OnLeave;
		}
	}
	else if ($touchdownon == 0)
	{
		if (select("Start a new Touch Down","No, thanks") == 1)
		{
			callsub OnJoin, 0;
		}
		else
		{
			goto OnLeave;
		}
	}
	else
	{
		next;
		mes .@npcname$;
		mes "Sorry, it's too late for this battle. Come back later.";
	}
	close;

OnEnterAgain:
	.@found1 = callfunc("CheckTDTeamRedPlayer");
	.@found2 = callfunc("CheckTDTeamBluePlayer");
	.@found = .@found1 + .@found2;
	if(.@found >= 1)
	{
		mes "Get ready, I'll send you back.";
		if(.@found1 == 1)
		{
			warp $touchdownMap$, $warpPointRedTD[0], $warpPointRedTD[1];
		}
		else
		{
			warp $touchdownMap$, $warpPointBlueTD[0], $warpPointBlueTD[1];
		}
	}
	else
	{
		mes "You're not part of this Touch Down.";
		mes "Wait for another to begin, please.";
	}
	close;

OnJoin:
	.@mode = getarg(0);

	.@npcname$ = "[Leag]";
	next;
	mes .@npcname$;
	mes "Sure!";
	mes "It'll cost you " + $TDEntryPrice + "z to enter.";
	if (Zeny < $TDEntryPrice)
	{
		next;
		mes .@npcname$;
		mes "Sorry, but you lack some money...";
		close;
	}
	if (select("Here you are.","I'll get back") == 1)
	{
		next;
		mes .@npcname$;
		Zeny = Zeny - $TDEntryPrice;
		mes "Allright.";
		if (.@mode == 0)
		{
			next;
			mes .@npcname$;
			mes "Now, choose a duration.";
			.@menu$ = "";
			for(.@i = 0; .@i<getarraysize($TDDuration); .@i++)
			{
				.@menu$ = .@menu$ + (.@i == 0 ? "" : ":") + $TDDuration[.@i] + " minutes";
			} 
			.@duration = select(.@menu$) - 1;
			next;
			mes .@npcname$;
			mes "Allright, let's go for " + $TDDuration[.@duration] + " minutes then.";
		}
		next;
		mes .@npcname$;
		mes "The team with the highest sum of points once the duration will be ellapsed will win.";
		mes "The reward will be the total amount paid by all players, divided between the team players.";
		mes "Also, there's an individual reward according to the number of kill a player may do.";
		if (.@mode == 0)
		{
			deletearray $@TDMembers[0], getarraysize($@TDMembers);
			$@TDMembers[0] = getcharid(3);
			
      bg_destroy $@blueTeam_TD;
      bg_destroy $@RedTeam_TD;
      bg_updatescore $touchdownMap$, 0, 0;
      
      bg_leave;
			$touchdownon = 1;
			$battleDuration = $TDDuration[.@duration];
			announce "[Touch Down] " + strcharinfo(0) + " just join the Touch Down. Who will come to fight him?",  bc_all, 0x00FF00;
		}
		else
		{
      bg_leave;
			$@TDMembers[getarraysize($@TDMembers)] = getcharid(3);
			if (getarraysize($@TDMembers) < $maxTDMemberCount)
			{
				announce "[Touch Down] " + strcharinfo(0) + " just join the Touch Down. Still " + ($maxTDMemberCount - getarraysize($@TDMembers)) + " fighters needed to begin!",  bc_all, 0x00FF00;
			}
			else
			{
				//start
				//build teams
				.teamCount = $maxTDMemberCount / 2;
				//warp first char
				announce "[Touch Down] The fight begins!", bc_all, 0x00FF00;
				.@warped = 0;
				$@blueTeam_TD = bg_create($touchdownMap$, $warpPointBlueTD[0], $warpPointBlueTD[1]);
				$@RedTeam_TD = bg_create($touchdownMap$, $warpPointRedTD[0], $warpPointRedTD[1]);
				for(.@i = 0; .@i<getarraysize($@TDMembers); .@i++)
				{
					if (attachrid($@TDMembers[.@i]) == 1)
					{	
						TDPOINTS = 0;
						getmapxy(.@map$, .@x, .@y, 0);
						savepoint .@map$, .@x, .@y;
						if (getarraysize($@tdTeamRed) < .teamCount)
						{
							$@tdTeamRed[getarraysize($@tdTeamRed)] = $@TDMembers[.@i];
							bg_join($@RedTeam_TD);
							setlook 7, getvariableofnpc(.redteam_color,"alabatd"); //alabadm
						}
						else if (getarraysize($@tdTeamBlue) < .teamCount)
						{
							$@tdTeamBlue[getarraysize($@tdTeamBlue)] = $@TDMembers[.@i];
							bg_join($@blueTeam_TD);
							setlook 7, getvariableofnpc(.blueteam_color,"alabatd");
						}
						else
						{
							if(rand(0,1))
							{
								$@tdTeamRed[getarraysize($@tdTeamRed)] = $@TDMembers[.@i];
								bg_join($@RedTeam_TD);
								setlook 7, getvariableofnpc(.redteam_color,"alabatd");
							}
							else
							{
								$@tdTeamBlue[getarraysize($@tdTeamBlue)] = $@TDMembers[.@i];
								bg_join($@blueTeam_TD);
								setlook 7, getvariableofnpc(.blueteam_color,"alabatd");
							}
						}
					}
				}
				donpcevent "alabatd::OnStartTD"; 
			}
		}
	}
	else
	{
		goto OnLeave;
	}
	close;

OnLeave:
	.@npcname$ = "[Leag]";
	next;
	mes .@npcname$;
	mes "Your choice!";
	close;
}

function	script	CheckTDTeamRedPlayer	{
	for(.@i = 0; .@i<getarraysize($@tdTeamRed) && .@found == 0; .@i++)
	{
		if($@tdTeamRed[.@i] == getcharid(3))
		{
			return 1;
		}
	}
	return 0;
}

function	script	CheckTDTeamBluePlayer	{
	for(.@i = 0; .@i<getarraysize($@tdTeamBlue) && .@found == 0; .@i++)
	{
		if($@tdTeamBlue[.@i] == getcharid(3))
		{
			return 1;
		}
	}
	return 0;
}

function	script	CheckTDPlayers	{
	for(.@i = 0; .@i<getarraysize($@TDMembers) && .@found == 0; .@i++)
	{
		if($@TDMembers[.@i] == getcharid(3))
		{
			return 1;
		}
	}
	return 0;
}